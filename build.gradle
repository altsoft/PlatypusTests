buildscript {
    repositories {
        jcenter()
    }
}

apply plugin: 'java'
apply plugin: 'war'

repositories {
    jcenter()
    mavenLocal()
}

configurations {
    compile.transitive = false
    testCompile.transitive = false
    pwc.transitive = false
    wrappers.transitive = false
    tomcat
    containerLibs
}

def tomcatVersion = '8.5.8'

dependencies {
    runtime 'platypus-js:platypus-js-servlet:5.11.0'
    pwc 'platypus-js:platypus-js-web-client:5.11.0'
    wrappers 'platypus-js:platypus-js-wrappers:5.11.0'
    tomcat "org.apache.tomcat:tomcat-catalina:$tomcatVersion",
           "org.apache.tomcat:tomcat-jasper:$tomcatVersion",
           "org.apache.tomcat:tomcat-dbcp:$tomcatVersion",
           "org.apache.tomcat:tomcat-websocket:$tomcatVersion",
           "org.apache.tomcat:tomcat-jaspic-api:$tomcatVersion"
    containerLibs 'com.h2database:h2:1.4.193'
}

def pwcDir = "${webAppDirName}/pwc"
def wrappersDir = "${webAppDirName}/WEB-INF/classes"
def libDir = "${webAppDirName}/WEB-INF/lib"

task cleanExplodePwc(type: Delete) {
    delete pwcDir
}

task cleanExplodeWrappers(type: Delete) {
    delete wrappersDir
}

task cleanLib(type: Delete) {
    delete libDir
}

clean.dependsOn cleanExplodePwc
clean.dependsOn cleanExplodeWrappers
clean.dependsOn cleanLib

task explodePwc(type: Copy) {
    configurations.pwc.each {
        from zipTree(file(it))
    }
    into pwcDir
}

task explodeWrappers(type: Copy) {
    configurations.wrappers.each {
        from zipTree(file(it))
    }
    into wrappersDir
}

task distilleLib(type: Copy) {
    configurations.runtime.each {
        from file(it)
    }
    into libDir
}

war {
    exclude 'platypus.xml'
    exclude 'private.properties'
    exclude 'project.properties'
}
war.dependsOn explodePwc
war.dependsOn explodeWrappers
war.dependsOn distilleLib

def tomcatDir = 'tomcat'
def tomcatBinDir = "${tomcatDir}/bin"
def tomcatLibDir = "${tomcatDir}/lib"

task buildTomcatBin(type: Copy) {
    configurations.tomcat.each {
        from file(it)
    }
    into tomcatBinDir
}

task buildTomcatLibs(type: Copy) {
    configurations.containerLibs.each {
        from file(it)
    }
    into tomcatLibDir
}

task buildTomcat() {
}

buildTomcat.dependsOn buildTomcatBin
buildTomcat.dependsOn buildTomcatLibs

task cleanTomcat(type: Delete){
    delete tomcatDir
}
clean.dependsOn cleanTomcat

def props = { 
    fileName ->
    def input = file(fileName).newDataInputStream()
    try {
        def loadedProps = new Properties()
        loadedProps.load(input)
        return loadedProps
    } finally {
        input.close()
    }
}

def appProps = props('src/main/webapp/project.properties')
def appPrivateProps = props('src/main/webapp/private.properties')

task completeTomcatConf(type: Copy){
    from 'src/main/tomcat-conf'
    into 'tomcat/conf'
    rename 'default-web.xml', 'web.xml'
}

task tomcatExec(type: JavaExec) {
    workingDir tomcatDir
    classpath fileTree(tomcatBinDir)
    main 'org.apache.catalina.startup.Bootstrap'
    jvmArgs "-Dtomcat.http.port=8080",
            "-Dapp.context=${appProps.context}",
            "-Dapp.base=${file('src/main/webapp')}"
}

task thinServerExec(type: JavaExec) {
    classpath fileTree(tomcatLibDir), fileTree('src/main/webapp/WEB-INF/lib'), 'src/main/webapp/WEB-INF/classes'
    main 'com.eas.server.ServerMain'
    args '-url', "${file('src/main/webapp').toURI().toURL()}", '-default-datasource', 'eas',
         '-datasource', 'eas', '-dburl', 'jdbc:h2:tcp/localhost/~/platypus-tests-eas', '-dbuser', 'sa', '-dbpassword', 'sa', '-dbschema', 'PUBLIC',
         '-datasource', 'easHR', '-dburl', 'jdbc:h2:tcp/localhost/~/platypus-tests-easHR', '-dbuser', 'sa', '-dbpassword', 'sa', '-dbschema', 'PUBLIC',
         '-source-path', "${appProps.sourcePath}",
         '-global-api'
}

task h2Exec(type: JavaExec) {
    classpath fileTree(tomcatLibDir)
    main 'org.h2.tools.Server'
    args '-tcp'
}

def tomcatServer
def thinServer
def h2Server
task launchServers(){
    doFirst {
        h2Server = new ProcessBuilder(h2Exec.commandLine)
                            .redirectErrorStream(true)
                            .start()
        def h2StdOut = new InputStreamReader(h2Server.getInputStream())
        def h2NextLine = h2StdOut.readLine()
        while(!h2NextLine.contains('TCP server running at')){
            println(h2NextLine)
            h2NextLine = h2StdOut.readLine()
        }
        println(h2NextLine)
        
        tomcatServer = new ProcessBuilder(tomcatExec.commandLine)
                            .directory(file(tomcatExec.workingDir))
                            .redirectErrorStream(true).start()
        def tomcatStdOut = new InputStreamReader(tomcatServer.getInputStream())
        def tomcatNextLine = tomcatStdOut.readLine()
        while(!tomcatNextLine.contains('Server startup in')){
            println(tomcatNextLine)
            tomcatNextLine = tomcatStdOut.readLine()
        }
        println(tomcatNextLine)
        
        thinServer = new ProcessBuilder(thinServerExec.commandLine)
                            .redirectErrorStream(true)
                            .start()
        def thinServerStdOut = new InputStreamReader(thinServer.getInputStream())
        def thinServerNextLine = thinServerStdOut.readLine()
        while(!thinServerNextLine.contains('Listening platypus protocol on')){
            println(thinServerNextLine)
            thinServerNextLine = thinServerStdOut.readLine()
        }
        println(thinServerNextLine)
    }
}

task shutdownServers (){
    doFirst {
        tomcatServer.destroy()
        tomcatServer.waitFor()
        println "Tomcat shutted down"
        thinServer.destroy()
        thinServer.waitFor()
        println "Thin server shutted down"
        h2Server.destroy()
        h2Server.waitFor()
        println "H2 server shutted down"
    }
}

launchServers.dependsOn buildTomcat
launchServers.dependsOn completeTomcatConf
launchServers.dependsOn distilleLib
launchServers.dependsOn explodeWrappers
launchServers.dependsOn explodePwc

test.dependsOn launchServers
launchServers.finalizedBy shutdownServers
shutdownServers.mustRunAfter test
